buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-docker-plugin:3.0.11'
  }
}

apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

repositories {
  mavenCentral()
}

task copyDistWebapp(type: Copy) {
  from('src/main/webapp')
  into('build/distributions/rest-o-rant-web')
}

build.dependsOn copyDistWebapp

dependencies {
  compile group: 'commons-io', name: 'commons-io', version: '1.4'
  compile group: 'log4j', name: 'log4j', version: '1.2.15', ext: 'jar'
}


docker {
   url = System.properties['docker.client.url']
   if (System.properties['docker.native'] == "false") {
        certPath=new File(System.properties['docker.cert.path'])
   }
   if (System.properties['docker.remote_registry'] == "true") {
     registryCredentials {
         url = System.properties['docker.client.url']
         username = System.properties['docker.registry.username']
         password = System.properties['docker.registry.password']
         email = System.properties['docker.registry.email']
     }
   }
}


task copyWebapp(type: Copy) {
  from('src/main/webapp')
  into('build/docker/webapp')
}

task copyNginx(type: Copy) {
  from('src/main/nginx')
  into('build/docker/nginx')
}

task createDockerfile(type: Dockerfile) {
  dependsOn copyWebapp, copyNginx
  destFile = project.file('build/docker/Dockerfile')
  from 'nginx'
  maintainer 'Vincent Partington <vpartington@xebialabs.com>'
  environmentVariable('API_ENDPOINT','http://rest-o-rant-api:8080/')
  copyFile('webapp', '/usr/share/nginx/html')
  copyFile('nginx', '/etc/nginx/conf.d')
  addFile('https://github.com/kreuzwerker/envplate/releases/download/v0.0.8/ep-linux', '/usr/local/bin/ep')
  runCommand('chmod +x /usr/local/bin/ep')
  instruction('CMD ["/usr/local/bin/ep", "-v", "/etc/nginx/conf.d/*.conf", "--", "/usr/sbin/nginx", "-g", "daemon off;"]')
}

task buildDockerImage(type: DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.parentFile
  tag = System.properties['docker.image.prefix']+"/rest-o-rant-web:$version"
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}

httpPort = 8080
stopPort = 9451
stopKey = 'stop-rest-o-rant-web'
